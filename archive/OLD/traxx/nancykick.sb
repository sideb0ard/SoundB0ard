
let blah_gen = gen()
{
  setup()
  {
    let blz = [3, 5, 3, 1, 3, 3, 0];
    let bx = 0;
    let beat = bjork(3,8);
    let notz = [30, 30, 30, 35, 30, 35];
    let nx = 0;
    let dnx = 0;
    let pp = 3840 / 8;
    let qt = 3840 / 4;
    let qtp = qt / 8;
    let iz = [2, 3, 4, 13];
    let ix = 0;

    let mnz = [12, 8, 4, 0];
    let mx = 0;
    let note_durz = [20, 30, 40, 50, 100];
    let ndx = 0;

  }
  run()
  {
    if ( count % 2 == 0) {
      beat = bjork(blz[bx], 8);
      bx = incr(bx, 0, len(blz));
    }
    if (count % 3 == 0) {
      beat = rotate(beat, bx);
    }

    if (count % 4 == 0) {
      let mel = rotate(combinor(beat, notz), 3);
      fast(bd, mel, 2);
    }

    for (i = 0; i < 8; ++i) {
      if (beat[i] == 1) {
        note_on_at(bd, notz[nx], i*pp);
        note_on_at(hh, notz[nx], i*qtp * iz[ix]);
        note_on_at(hh, notz[nx], (2 * qt) + i*qtp + iz[ix]);

        note_on_at(dx100, notz[dnx] - mnz[mx], i*pp, dur=note_durz[ndx]);
        ndx = incr(ndx, 0, len(note_durz));

        ix = incr(ix, 0, len(iz));
      }
    }
    nx = incr(nx, 0, len(notz));
    if (count % 8 == 0) {
      dnx = incr(dnx, 0, len(notz));
    }
    if (count % 7 == 0) {
      mx = incr(mx, 0, len(mnz));
    }

    if (count % 7 == 0) {
      let mel = rotate(combinor(beat, notz), 3);
      fast(dx100, mel, 0.25);
    }

    #note_on(mo, notes_in_chord(notz[dnx], 0, true),  dur=note_durz[ndx]);

  }
}

let dt = sample(perc/DUT.aiff);
vol dt 0.2;
let cl = sample(perc/808clave.aif);
vol cl 0.1;
let sf = sample(perc/surfaceFalle.aiff);
vol sf 0.3;

let sablah_gen = gen()
{
  setup()
  {
    let beat = rand_beat();
    let sampz = [dt, cl, sf];
    let sx = 0;
  }
  run()
  {

    if (count % 4 == 0) {
      for (i = 0; i < 16; ++i) {
        if (beat[i] == 1) {
          note_on_at(sampz[sx], 1, i * pp);
          sx = incr(sx, 0, len(sampz));
        }
      }
    }
    if (count % 3 == 0) {
      play_array(sampz[sx], beat);
    }
    if (count % 7 == 0) {
      beat = rand_beat();
    }
  }
}

