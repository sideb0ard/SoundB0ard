load_dir("aesamples");

bpm 131;


let kick_gen = gen()
{
  setup()
  {
    let bdz = [SH09_KIK, SPSKikA];
    # let bdz = [SH09_KIK];
    let bx = 0;
    let beat = rand_beat();
    print("BEAT IS ", beat);
  }
  run()
  {
    if (count % 3 == 0) {
      beat = rand_beat();
    }

    if (count % 8 < 7) {
      for (i = 0; i < 16; ++i) {
        if (beat[i] == 1) {
          note_on_at(bdz[bx], 1, pp*i);
          if (rand(100) > 70) {
            bx = incr(bx, 0, len(bdz));
          }
        }
      }
    }

  }
}

vol SH09_HAT 0.3;
vol steeldif09 0.3;
vol steelxif18 0.3;
vol steelxif02 0.3;

let perc_gen = gen()
{
  setup()
  {
    let pattern = rand_array(16, 0, 6);
    let percz = [SPSKikB, steelxif18, steeldif09, steelxif02, SH09_SNRZ, SH09_HAT];
    let plen = 1;
  }
  run()
  {
    if (count % 8 == 0) {
      pattern = rand_array(16, 0, 6);
    }
    for (i = 0; i < 16; ++i) {
      if (pattern[i] < plen) {
        note_on_at(percz[pattern[i]], 1, i*pp);
      }
    }
    plen = incr(plen, 1, len(percz));
    pattern = rotate(pattern, 3);
  }
}

let noise_gen = gen()
{
  setup()
  {
    let noizes = [SHISTASample06PP, SH09_ACID, SHSTR_26x];
    let nidx = 0;
    let pitchz = [0.8, 1, 1.5, 1.7, 2];
    let pix = 0;
  }
  run()
  {
    if (count % 3 == 0) {
      note_on(noizes[nidx], 1);
      nidx = incr(nidx, 0, len(noizes));
    }
    if (count % 4 == 0) {
      set_pitch(noizes[nidx], pitchz[pix]);
      pix = incr(pix, 0, len(pitchz));
    }
  }
}

let melo_gen = gen()
{
  setup()
  {
    let notes = gimme_notes();
    let nidx = 0;
    let rhythm = rand_beat();
    let qpp = 3840 / 4;
    let weepp = pp / 16;
  }
  run()
  {
    if (count % 4 == 0 || count % 3 == 0) {
      let offset = rand(4) * qpp;
      for (i = 0; i < 16; ++i) {
        if (rhythm[i] == 1) {
          let tick = offset + weepp * i;
          note_on_at(dxx, notes[nidx], tick);
          if (rand(100) > 50) {
            nidx = incr(nidx, 0, len(notes));
          }
        }
      }
    }
  }
}

let hh = sample(garage/HHCH-606_80.wav);
let hh_gen = gen()
{
  setup()
  {
    let swing = 12;
    let vel = 110;
  }
  run()
  {

    if (rand(100) > 10) {
      for (i = 0; i < 16; ++i) {
        let tick = pp * i;
        if (count % 3 == 0 && i % 2 == 0) {
          tick += swing;
          swing = incr(swing, 124, 12);
        }
        note_on_at(hh, 1, tick, vel);
        vel = incr(vel, 67, 128);
      }
    }
  }
}

# let strll = loop(aesamples/shstr_04.WAV);

let str_gen = gen()
{
  setup()
  {
    let volx = 0.1;
    let i = 0.05;

  }
  run()
  {
    vol strll volx;
    volx = volx + i;
    if (volx > 0.4) {
      volx = 0;
    }

  }
}

