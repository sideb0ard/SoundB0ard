# f
#let key = 40;
#let key = 30;
#let key = 30;
let key = 14;

let jdy = sample(aaassembly/heyJODY.wav);

# bpm 110 ; let intr = loop(legowelt/Synth-InterBellumAtmo.wav); set intr:len 2;
# let rkk = loop(dloops/rockafellayall2bar.aiff); set rkk:len 2;
# let rks = loop(dloops/rocksteady_vocal.aif);  p2 $ jblah_gen
# let cr = loop(loops2/crdunk.wav); set cr:len 2 ; set cr:vol 0 at=3840*2;

let jblah_gen = gen()
{
  setup()
  {
    let d = 10000;
    let rklen = 1;
  }
  run()
  {

    set cr:vol 0;
    if (count % 8 == 0) {
      let b = rand_beat();
      for (i = 0; i < 16; ++i) {
        if (b[i] == 1) {
          note_on_at(jdy, 1, i * pp, dur = d);
        }
      }
      d = d + 1000;
      if (d > 50000) {
        d = 100;
      }
    }
    if (count % 8 == 7) {
      set rkk:stutter 1;
      set cr:vol 1 at=pp*8;
    }
    if (count % 8 == 6) {
      set rks:reverse 1;
      set cr:vol 1 at=pp*4;
    }
    if (count % 3 == 0) {
      set rks:len 1 + rklen;
      rklen = 1 - rklen;
    }

    if (count % 8 == 5) {
      set intr:vol 0;
      mashup([rkk, intr, rks]);
    }
    if (count % 12 == 11) {
      set intr:vol 0.7;
      solo(intr, at=8*pp);
      set cr:vol 1;
    }
  }
}
# p4 < osc 8 "0 0.5" "send(0, [jdy, intr], %)"
# p5 < osc 7 "0 0.5" "send(1, [jdy, intr], %)"

# let jb = loop(aaassembly/jungleBREAKDOWN.wav); set jb:len 4;

# per1 - GHOSTXZ //
# load_preset(per1, "GHOSTXZ") ;p6 $ bassblah_gen
let bassblah_gen = gen()
{
  setup()
  {
    let mel1 = [30, 0, 0, 32, 0, 0, 32, 0, 0, 40, 0, 40, 41, 0, 30, 0];
    let mel2 = [40, 0, 0, 33, 0, 40, 0, 41, 0, 0, 32, 0, 40, 0, 30, 0];
    let melz = [mel1, mel2];
    let mx = 0;

    let midx = 0;
    let midxmax = 3;

    let dr = 200;
  }
  run()
  {
    if (count % 3 == 0) {
      mx = incr(mx, 0, len(melz));
    }
    if (count % 4 > 1) {
      let mel = melz[mx];
      for (i = 0; i < 16; ++i) {
        if (mel[i] > 0) {
          note_on_at(per1, mel[i], i * pp, dur = dr);
        }
      }
    }
    dr = dr + 50;
    if (dr > 400) {
      dr = 100;
    }
  }
}

# dx = wuurpr // fnc // l0renzo4 // wuuurodx
let dxblah_gen = gen()
{
  setup()
  {
    #let ocz = [ 4, 16];
    let ocz = [ 30, 40];
    let ox = 0;
    let dxmel = gamelody(16, ocz[ox]);
    let sx = 0;
    let smx = 3;
    let dr = 100;
  }
  run()
  {

    if (count % 8 < 6) {
      for (i = 0; i < 16; ++i) {
        if (dxmel[i] > 0) {
          let v = 80 + rand(47);
          if (i % 4 == 2) {
            v = 127;
          }
          note_on_at(dx, dxmel[i], i*pp, vel = v, dur = dr);
        }
      }
    }
    if (count % 12 == 0) {
      let dxmel = gamelody(16, ocz[ox]);
      ox = incr(ox, 0, len(ocz));
    }
    if (count % 8 == 6) {
      for (i = 0; i < 16; ++i) {
        stepper(dx, dxmel, sx, i, dr);
        sx = incr(sx, 0, smx);
      }
    }
    smx = incr(smx, 3, 16);
    dr = dr + 20;
    if (dr > 270) {
      dr = 50;
    }
  }
}

# load_preset(dx, "lowr"); p6 $ GREENgen
# dx = lowr
# RAMONES - "Blietzkrieg Pop" B5 – E5 – F#5 // I - IV - V
# GREEN DAY - "When i come around" G5 D5 Em C5 // I V vi IV
let GREENgen = gen()
{
  setup()
  {
    let ntzk = notes_in_key(key);
    let uppad = 0;
    let I = notes_in_chord(ntzk[0] + uppad, 3);
    let V = notes_in_chord(ntzk[4] + uppad, 3);
    let vi = notes_in_chord(ntzk[5] + uppad, 1);
    let IV = notes_in_chord(ntzk[3] + uppad, 3);

    let d = 70;
    let sw = 40;
  }
  run()
  {
    let mel = combinorCh(bjork(5, 8), I) + combinorCh(bjork(5, 8), V);;
    if (count % 2 == 1) {
      mel = combinorCh(bjork(4, 8), [vi, IV]) + combinorCh(bjork(3, 8), IV);;
    }
    for (i = 0; i < 16; ++i) {
      if (mel[i] != 0) {
        note_on_at(dx, mel[i], i * pp, dur = 200);
      }
    }
  }
}

let bodylah_gen = gen()
{
  setup()
  {
  }
  run()
  {
    if (count % 4 == 3) {
      set ab:stutter 1;
      set rks:vol 0;
      set rkk:stutter 1;
    }
    if (count % 4 == 0) {
      set ab:scramble 1;
      set rks:vol 0.7;
      set rkk:scramble 1;
    }
    set rks:stutter 1;
  }
}

let kd = sample(wavalc/dm02_909_kickdrum_005_clean.wav);
let hh1 = sample(ch/SNDhat.aiff);
let hh2 = sample(ch/krushch.wav);

let hhblah_gen = gen()
{
  setup()
  {

    let hhz = rand_array(16, 0, 2);
  }
  run()
  {
    if (count % 8 < 6) {

      for (i = 0; i < 16; ++i) {
        if (i % 4 == 0) {
          note_on_at(kd, 1, i * pp, dur = 100);
        }
      }
    } else {
      play_array(kd, rand_beat());
    }

    if (count % 8 == 0) {
      hhz = rand_array(16, 0, 2);
    }

    for (i = 0; i < 16; ++i) {
      if (hhz[i] == 0) {
        note_on_at(hh1, 1, i * pp, dur = 10);
      }
      if (hhz[i] == 1) {
        note_on_at(hh2, 1, i * pp, dur = 10);
      }
    }
  }
}

