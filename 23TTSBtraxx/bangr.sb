# bpm 150
# kit();
# load_preset(bd, "KICKR"); load_preset(cb, "oopdz"); load_preset(cl, "glotch");
# load_preset(cp, "scicczr"); load_preset(hh, "snipz");  load_preset(oh, "OH");
# load_preset(sd, "SNDrm"); load_preset(dx, "purlp");

# I-vi-IV-V
# key of E // 16 // 28
let progz = [ [28, 32, 35], [33, 35, 40], [30, 35, 37], [32, 35, 39] ];
let prgx = 0;
let dabeat = rand_beat();

# p4 < ramp 8 "0 0.5" "vol cl %; vol cp %; vol cb %";
let bdumblah_gen = gen()
{
  setup()
  {
  }
  run()
  {
    let beat = dabeat;
    if (count % 8 == 7)  {
      beat = mask(dabeat, rand_array(16, 0, 1));
    }
    if (rand(100) > 95) {
      beat = bjork(3, 8);
    }

    if (count % 16 < 15) {
      for (i = 0; i < 16; ++i) {
        if (beat[i] == 1) {
          let v = 80 + rand(47);
          let off = rand(50);
          if (i == 0) {
            v = 127;
            off = 0;
          }
          note_on_at(bd, scale_note(12, 16), i * pp + off, vel = v);
        }
      }
    }

    if (count % 4 == 0) {
      dabeat = rand_beat();
    }
    if (count % 8 == 0) {
      prgx = incr(prgx, 0, len(progz));
    }
  }
}

let bdumblah2_gen = gen()
{
  setup()
  {
  }
  run()
  {
    let beat = bjork(3, 8);

    for (i = 0; i < 16; ++i) {
      if (beat[i] == 1) {
        note_on_at(bd, 12, i * pp);
      }
    }

  }
}


let perc1blah_gen = gen()
{
  setup()
  {
    let percz = [cp, cb, cl];
    let px = 0;

    let lnz = [50, 100, 200, 400];
    let lx = 0;
  }
  run()
  {
    let ibeat = mask(invert(dabeat), rand_array(16, 0, 1));

    let plx = 0;
    if (count % 18 < 17) {
      for (i = 0; i < 16; ++i) {
        if (ibeat[i] == 1) {
          note_on_at(percz[px], progz[prgx][plx], i * pp, dur = lnz[lx], vel = 90 + rand(37));

          if (i % 3 == 0) {
            px = incr(px, 0, len(percz));
          }
          if (i % 2 == 0) {
            plx = incr(plx, 0, len(progz[prgx]));
          }
          lx = incr(lx, 0, len(lnz));
        }
      }
    }

  }
}


## hh = snipz // oh = OH
let htz = [hh, oh];
let hhblah_gen = gen()
{
  setup()
  {
    let dapat = rand_array(16, 0, 1);
  }
  run()
  {
    if (count % 7 == 0) {
      dapat = rand_array(16, 0, 1);
    }

    let pat = dapat;
    if (count % 3 == 0) {
      pat = mask(dapat, dabeat);
    }

    let played_open_count = 0;
    if (count % 8 < 7) {
      for (i = 0; i < 16; ++i) {
        let v = 80 + rand(37);
        let off = 50;
        if (i % 8 == 0) {
          v = 120;
        }
        if (i % 2 == 0) {
          off = 0;
        }
        if (pat[i] == 1) {
          note_on_at(hh, progz[prgx][0], i * pp + off, vel = v);
        } else {
          if (count % 2 == 0 && played_open_count < 4) {
            if (rand(100) > 70) {
              note_on_at(oh, progz[prgx][0]+12, i * pp);
              ++played_open_count;
            }
          }
        }
      }
    }
  }
}

let cblah_gen = gen()
{
  setup()
  {
    let probz = {1:30, 4:90, 7:10, 12:90, 14:10};
    #let probz = { 4:90, 8:90};
  }
  run()
  {
    if (count % 3 == 0) {
      for (i = 0; i < 16; ++i) {
        if (probz[i]) {
          if (rand(100) < probz[i]) {
            let v = 100 + rand(27);
            let off = rand(30) - 15;
            if (i % 8 == 4) {
              v = 127;
            }
            note_on_at(sd, progz[prgx][0]+12, i * pp - off, vel = v);
          }
        }
      }
    }
  }
}


## let dx = fm(); load_preset(dx, "purlp");
#
## wuurpr // landroiD
## p3 < osc 8 "50 90"  "set dx:op3out %"
## p4 < osc 3 "50 90"  "set dx:op4out %"
## p5 < osc 1 "50 90"  "set dx:op2out %"
## purlp // SBBZzz // whippit // sawrr // drmrrr
let wwblah_gen = gen()
{
  setup()
  {
    let t = 388744;
    let dr = 100;
    let mmax = 16;

  }
  run()
  {
    if (count % 1 == 0) {
      let p = bits( t>>( 8 / ( ( t / 32) % 3)) );
      ++t;

      for (i = 0; i < mmax; ++i) {
        if (p[i] > 0) {
          note_on_at(dx, 28, i * pp , dur = dr);
          dr = dr + 20;
          if (dr > 300) {
            dr = 70;
          }
        }
      }
      mmax = mmax - 1;
      if (mmax < 8) {
        mmax = 16;
      }
    }
  }
}

let wwblah_gen2 = gen()
{
  setup()
  {
    let dr = 500;
    let melly = gamelody(16, 28);
    let mx = 0;
    let mmax = 3;

  }
  run()
  {
    if (count % 16 == 0) {
      melly = gamelody(16, 28);
    }
    let mel = melly;
    if (count % 4 == 3) {
      mel = mask(melly, dabeat);
    }
    for (i = 0; i < 16; ++i) {
      stepper(dx, mel, mx, i, 100);
      mx = incr(mx, 0, mmax);
    }
    mmax = incr(mmax, 3, 16);
  }
}

##load_preset(per1, "DWUB"); load_preset(per2, "wllopa"); load_preset(per3, "WWWWWl");
##vol per1 0.4; vol per2 0.4; vol per3 0.4;
let microblah_gen = gen()
{
  setup()
  {
    let perkx = [per1, per2, per3];
    let pkx = 0;
  }
  run()
  {
    if (count % 2 == 1) {
      let lpp = pp / 8;
      for (i = 8; i < 15; ++i) {
        if (dabeat[i] == 0) {
          let b = rand_array(4, 0, 1);

          for (j = 0; j < 4; ++j) {
            if (b[j] == 1) {
              note_on_at(perkx[pkx], progz[prgx], i*pp + j * lpp);
              pkx = incr(pkx, 0, len(perkx));
            }
          }
        }
      }
    }
  }
}


# BLELL
let deeblah_gen = gen()
{
  setup()
  {
  }
  run()
  {
    if (count % 3 == 1) {
      let pt1 = rand_array(12, 0, 1);
      for (i = 0; i < 12; ++i) {
        if (pt1[i] == 1) {
          note_on_at(bb, progz[prgx][0]+12, i*pp);
        }
      }
    }

    if (count % 6 == 0) {
      let pt2 = rand_array(8, 0, 1);
      let lpp = 3840 / 32;
      for (i = 0; i < 8; ++i) {
        if (pt2[i] == 1) {
        }
        note_on_at(bb, progz[prgx][1]+12, 12*pp + i* lpp);
      }
    }
  }
}

